/*
 *
 *       Auteur: Pedrono Marie-Jo
 *
 * 	cr�ation : 13 f�vrier 2002
 *
 *     nature : projet etudiant
 *
 */


options{
	JDK_VERSION="1.5";	
}

PARSER_BEGIN(Yaka)



public class Yaka {
 static TabIdent tab = new TabIdent();
 static YVM fichYVM = new YVM("Fichier YVM",tab);
 static YVMasm fichYVMasm = new YVMasm("Fichier YVMasm",tab);
 static Expression exp = new Expression(fichYVM,fichYVMasm);
 static Declaration dec = new Declaration(fichYVM, fichYVMasm);

  public static void main(String args[]) { 
    Yaka analyseur;
    java.io.InputStream input;

    if (args.length==1) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]+".yaka");
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
    } else if (args.length==0) {
      System.out.println("Lecture sur l'entree standard...");
      input = System.in;
    } else {
      System.out.println("Usage: java Gram [fichier]");
      return;
    }
    try {
      analyseur = new Yaka(input);
      analyseur.analyse();
      System.out.println("analyse syntaxique reussie!");
    } catch (ParseException e) {
      String msg = e.getMessage();
      msg = msg.substring(0,msg.indexOf("\n"));
      System.out.println("Erreur de syntaxe : "+msg);
    }
  } 
}

PARSER_END(Yaka)
/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu,chaineLue;
 public static int entierLu;}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots r�serv�s*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
        }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire********/
/**************************************/
void analyse() : {}
{
   <PROGRAMME>{exp.entete();} <ident>
   (declFonction())*
   < PRINCIPAL >	
   bloc()
   < FPRINCIPAL >
   <FPROGRAMME>{exp.queue();}
}

/*
*FONCTIONS
*/

void declFonction() : {}{
  type() < FONCTION > < ident >{dec.identLu=YakaTokenManager.identLu;} paramForms(){dec.placerParam(tab);dec.placerFonct(tab);} bloc() < FFONCTION >{exp.fermeBloc(tab);}
}

void paramForms() : {}{
"("(paramForm() (","paramForm())*)?")"
}

void paramForm() : {}{
type(){dec.typeStack.push(dec.typeLu);dec.paramType.add(dec.typeLu);} < ident >{dec.nomStack.push(YakaTokenManager.identLu);}
}

void retourne() : {}{
< RETOURNE > expression(){exp.ireturn(tab);}
}

void argumentsFonction() : {}{
"("{exp.call(tab,exp.nomFonction);}(expression() (","expression())*)?")"
}

/*
*FIN FONCTION
*/

void bloc() : {}{
 (declConst())*
 (declVar())* {exp.ouvrePrinc();}		
   suiteInstr() 
}

void declConst() : {}
{
  <CONST>  defConst() ( "," defConst())* ";"
}

void defConst() : {}
{
	
  <ident> {dec.identLu=YakaTokenManager.identLu;} 	
   "=" valConst()
    
}

void valConst() : {}{
  <entier> {dec.placerConst(dec.identLu,YakaTokenManager.entierLu,Constante.type.ENTIER,tab);}
 | <ident>	{dec.placerConst(dec.identLu,tab.chercheIdentLocaux(YakaTokenManager.identLu).getValeur(),tab.chercheIdentLocaux(YakaTokenManager.identLu).getType(),tab);}	
 | <VRAI> 	{dec.placerConst(dec.identLu,/*Constante.bool.VRAI*/-1,Constante.type.BOOL,tab);}	
 | <FAUX> 	{dec.placerConst(dec.identLu,/*Constante.bool.VRAI*/0,Constante.type.BOOL,tab);}	
}


void declVar() : {}
{
  <VAR> type()	
  <ident> {dec.placerVariable(YakaTokenManager.identLu,tab.iterateurVariable,dec.typeLu,tab);}	
  ("," 
      <ident> {dec.placerVariable(YakaTokenManager.identLu,tab.iterateurVariable,dec.typeLu,tab);} 
  )* ";"
	
}

void type() :{}
{
   <ENTIER> {dec.typeLu=Constante.type.ENTIER;}		
 | <BOOLEEN> {dec.typeLu=Constante.type.BOOL;}	
}

/*
 * Instruction
 */
void suiteInstr() :{}
{
	instruction() (";"(instruction())?)*}

void instruction() :{}  {	affectation() | lecture() | ecriture()  | iteration() | condition() | retourne()
}

void affectation() :{}
{	<ident>{exp.idLu=YakaTokenManager.identLu;} "=" expression(){exp.affecter(tab.chercheIdentLocaux(exp.idLu));}
}

void lecture() :{}
{
	< LIRE >"("<ident>")" {exp.lire(tab.chercheIdentLocaux(YakaTokenManager.identLu));}
}


void ecriture() :{}
{
	< ECRIRE >"(" ( expression(){exp.ecrire();} | <chaine> {exp.ecrire(YakaTokenManager.chaineLue);} ) ")" | < ALALIGNE > {exp.aLaLigne();}
}
/*
 * Iteration
 */
void iteration() : {}
{ <TANTQUE >{exp.tantQue();} expression(){exp.faire();} < FAIRE > suiteInstr() < FAIT >{exp.fait();} }

/*
* Conditionnelle
*/

void condition() : {}
{< SI >{exp.si();} expression(){exp.alors();} <ALORS > suiteInstr(){exp.sinon();} (< SINON > suiteInstr())? < FSI >{exp.fsi();}
}

/*
 * Expression .
 */
void expression() : {}
{
  simpleExpr() 
  (opRel() 
  simpleExpr() {exp.OppRel();}
  )?
}



void simpleExpr() : {}
{
   terme() 
   (opAdd()
   terme()  {exp.OppAdd();}
   )*
} 
void terme() : {}
{
  facteur() 
  (opMul()
  facteur()	{exp.OppMul();}
  )*
}
void facteur() : {}
{   primaire() 
  |  (opNeg() primaire()) {exp.OppNeg();} 
}

void primaire (): {}
{
   valeur()
 | "(" expression() ")" 
 
}

void valeur () : {}
{ <entier> {exp.empileType('e');
			exp.ecrireEntier(YakaTokenManager.entierLu);}
 | <ident>{	exp.empileType(tab.chercheIdent(YakaTokenManager.identLu).getTypeToChar());
			exp.ecrireIdent(tab.chercheIdent(YakaTokenManager.identLu));
			exp.nomFonction=YakaTokenManager.identLu;} (argumentsFonction())?
 | <VRAI>  {exp.empileType('b');
 			exp.ecrireBool(Constante.bool.VRAI);}
 | <FAUX>  {exp.empileType('b');
 			exp.ecrireBool(Constante.bool.FAUX);}
 
}

void opRel() : {}
{
   "=" {exp.empileOp('=');}
 | "<>"{exp.empileOp('d');}
 | "<" {exp.empileOp('<');}
 | "<="{exp.empileOp('i');}	
 | ">" {exp.empileOp('>');}		
 | ">="{exp.empileOp('s');}	
} 

void opAdd() : {}
{
    "+"	{exp.empileOp('+');}
  |  "-" {exp.empileOp('-');}  
  | <OU> {exp.empileOp('o');}
}

void opMul() : {}
{
  "*" {exp.empileOp('*');}		
  | "/"	{exp.empileOp('/');}
  | <ET> {exp.empileOp('a');}
} 

void opNeg() : {}
{ "-"	{exp.empileOp('-');}	
 | <NON> {exp.empileOp('n');}	
}


